apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
        }
    }
    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Added for AdMob
    compile project(':jokeslib')
    compile project(':displayjokeactivity')
    compile 'com.android.support:appcompat-v7:23.1.1'
    freeCompile 'com.google.android.gms:play-services-ads:8.3.0'
    compile project(path: ':backend', configuration: 'android-endpoints')
}

task launchGCELocal (dependsOn: ':backend:appengineRun') {
    evaluationDependsOn ":backend"
    project(':backend') {
        appengine {
            daemon = true
        }
    }
    doLast {
        println "Local GCE Server is started !"
    }
}

task testJokeLib {
    dependsOn ':jokeslib:compileJava'
    dependsOn ':jokeslib:testClasses'
}

task testGCELocal {
    dependsOn ':app:connectedAndroidTest'
}

task shutdownGCE {
    dependsOn ':backend:appengineStop'
}

task localTest {
    dependsOn launchGCELocal
    dependsOn testJokeLib
    dependsOn testGCELocal
    dependsOn shutdownGCE
    launchGCELocal.mustRunAfter testJokeLib
    testGCELocal.mustRunAfter launchGCELocal
    shutdownGCE.mustRunAfter testGCELocal
    doLast {
        println "localTest is done."
    }
}


